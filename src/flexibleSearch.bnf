/*
 * Copyright 2012-2013 Brandon Kearby
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

//
// FlexibleSearch BNF Grammar
//
{
  parserClass="com.intellij.idea.plugin.hybris.flexibleSearch.FlexibleSearchParser"
  parserUtilClass="com.intellij.idea.plugin.hybris.flexibleSearch.utils.FlexibleSearchParserUtils"
  
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="FlexibleSearch"
  psiImplClassSuffix="Impl"
  psiPackage="com.intellij.idea.plugin.hybris.flexibleSearch.psi"
  psiImplPackage="com.intellij.idea.plugin.hybris.flexibleSearch.psi.impl"

  elementTypeHolderClass="com.intellij.idea.plugin.hybris.flexibleSearch.psi.FlexibleSearchTypes"
  elementTypeClass="com.intellij.idea.plugin.hybris.flexibleSearch.psi.FlexibleSearchElementType"
  tokenTypeClass="com.intellij.idea.plugin.hybris.flexibleSearch.psi.FlexibleSearchTokenType"
 
  tokens = [
        PERCENT                         = '%'
        QUOTE                           = "'"
        ASTERISK                        = "*"
        PLUS_SIGN                       = "+"
        COMMA                           = ","
        MINUS_SIGN                      = "-"
        DOT                             = "."
        COLON                           = ":"
        SEMICOLON                       = ";"
        QUESTION_MARK                   = "?"
        EXCLAMATION_MARK                = "!"
        UNDERSCORE                      = "_"

        LEFT_BRACKET                    = "["
        RIGHT_BRACKET                   = "]"
        LEFT_PAREN                      = "("
        RIGHT_PAREN                     = ")"
        LEFT_BRACE                      = "{"
        RIGHT_BRACE                     = "}"
        RIGHT_DOUBLE_BRACE              = "regexp:(}})"
        LEFT_DOUBLE_BRACE               = "regexp:({{)"
        
        EQUALS_OPERATOR                 = 'regexp:(=)'
        NOT_EQUALS_OPERATOR             = 'regexp:(<>)'
        GREATER_THAN_OPERATOR           = '>'
        GREATER_THAN_OR_EQUALS_OPERATOR = "regexp:(>=)"
        LESS_THAN_OPERATOR              = "<"
        LESS_THAN_OR_EQUALS_OPERATOR    = "regexp:(<=)"


        SPACE                           = 'regexp:\s+'
        IDENTIFIER                      = "regexp:([:jletter:] [:jletterdigit:]*)"
        COLUMN_REFERENCE_IDENTIFIER     = "regexp:([:jletter:] [:jletterdigit:]*)"
        TABLE_NAME_IDENTIFIER           = "regexp:([:jletter:] [:jletterdigit:]*)"
        STRING                          = "regexp:('([^'\\]|\\.)*'|\"([^\"\\]|\\\"|\\\'|\\)*\")"
        NUMBER                          = "regexp:[:jdigit:]*"
  ]
}

flexibleSearchFile ::= statement*

private statement ::= !<<eof>> (query_specification empty_statement?)* empty_statement?
{recoverWhile = expressionRecoverWhile }
 
private expressionRecoverWhile ::= !(SEMICOLON | <<eof>> )

private empty_statement ::= SEMICOLON | <<eof>>

// Specify a table derived from the result of a <table expression>.

query_specification ::= SELECT [ set_quantifier ] select_list table_expression
{pin = 1}

select_list ::= ASTERISK | select_sublist [ ( COMMA select_sublist )* ]

select_sublist ::= derived_column | (aggregate_function [COMMA aggregate_function*])

derived_column ::= value_expression [ AS  correlation_name ]

set_quantifier ::= (DISTINCT | ALL)

aggregate_function ::=
		COUNT LEFT_PAREN ASTERISK RIGHT_PAREN 
	|	general_set_function 

general_set_function ::= set_function_type LEFT_PAREN [ set_quantifier ] value_expression RIGHT_PAREN [ [ AS ] correlation_name ]

set_function_type ::= computational_operation

private computational_operation ::=
		AVG | MAX | MIN | SUM
	|	EVERY | ANY | SOME
	|	COUNT

table_expression ::= from_clause 
		 where_clause? 
		 order_by_clause? 
		 group_by_clause?

order_by_clause ::= ORDER BY sort_specification_list
{pin = 1 recoverWhile = orderByClauseRecoverWhile}

private orderByClauseRecoverWhile ::= !( SEMICOLON | query_specification | RIGHT_DOUBLE_BRACE)

sort_specification_list ::= sort_specification [ { COMMA sort_specification }* ]

sort_specification ::= sort_key [ ordering_specification ] [ null_ordering ]

sort_key ::= value_expression

ordering_specification ::= ASC | DESC

null_ordering ::= NULLS FIRST | NULLS LAST


//Specify a table derived from one or more tables. <from clause>

//<from clause> ::= FROM <table reference list>
from_clause ::= FROM (LEFT_BRACE table_reference_list RIGHT_BRACE | subquery )
{pin = 1}

table_reference_list ::=  table_reference [ { COMMA? table_reference }* ] 

table_reference ::= table_primary [joined_table] | joined_table

table_primary ::= table_name [ [ AS ] correlation_name ]

table_name ::= TABLE_NAME_IDENTIFIER [ EXCLAMATION_MARK ]
{
    pin=1 
    mixin = "com.intellij.idea.plugin.hybris.flexibleSearch.psi.references.TypeNameMixin"
}

correlation_name ::= IDENTIFIER

// 7.7 <joined table> (p312)
// Specify a table derived from a Cartesian product, inner or outer join, or union join.

joined_table ::= [(table_primary [joined_table] | joined_table)] [ join_type ] JOIN table_reference join_specification 
{pin = 3}

join_specification ::= join_condition 

join_condition ::= ON search_condition
{pin = 1}

join_type ::= LEFT |


table_subquery ::= subquery

subquery ::= 
LEFT_PAREN? LEFT_DOUBLE_BRACE query_specification RIGHT_DOUBLE_BRACE [(UNION ALL subquery)*]  RIGHT_PAREN? [ [ AS ] correlation_name ]


where_clause ::= WHERE search_condition

search_condition ::= boolean_value_expression

// boolean_value_expression 
boolean_value_expression ::= boolean_term

boolean_term ::= boolean_factor (((AND|OR) boolean_term)*)?
{pin = 1}

boolean_factor ::= [ NOT ] boolean_test

boolean_test ::= boolean_primary  [ IS [ NOT ] truth_value ]

truth_value ::= TRUE | FALSE 

boolean_primary ::= boolean_predicand | predicate 

/*
boolean_predicand ::= parenthesized_boolean_value_expression

parenthesized_boolean_value_expression ::= LEFT_PAREN boolean_value_expression LEFT_PAREN

nonparenthesized_value_expression_primary ::= id
*/

boolean_predicand ::= parenthesized_boolean_value_expression | nonparenthesized_value_expression_primary

private parenthesized_boolean_value_expression ::= LEFT_PAREN boolean_value_expression RIGHT_PAREN

private nonparenthesized_value_expression_primary ::= column_reference
//Predicates
//Specify a condition that can be evaluated to give a boolean value.

predicate ::= comparison_predicate
	|	between_predicate
	|	like_predicate
	|	null_predicate
	|	exists_predicate

null_predicate ::= row_value_predicand IS [ NOT ] NULL

exists_predicate ::= EXISTS table_subquery

//Specify a comparison of two row values. <comparison predicate>
private comparison_predicate ::= row_value_predicand comp_op row_value_predicand

comp_op ::=
		EQUALS_OPERATOR
	|	GREATER_THAN_OPERATOR
	|	NOT_EQUALS_OPERATOR
	|	LESS_THAN_OPERATOR
	|	LESS_THAN_OR_EQUALS_OPERATOR
	|	GREATER_THAN_OR_EQUALS_OPERATOR


// Specify a range comparison. <between predicate>
between_predicate ::= row_value_predicand [ NOT ] BETWEEN row_value_predicand AND row_value_predicand

row_value_predicand ::= value_expression | common_value_expression

common_value_expression ::= string_value_expression

like_predicate ::= character_like_predicate

character_like_predicate ::= row_value_predicand [ NOT ] LIKE (character_pattern | value_expression)

character_pattern ::=  STRING | string_value_function

string_value_expression ::=  general_literal

general_literal ::= character_string_literal

character_string_literal ::= STRING 

string_value_function ::= character_value_function 

character_value_function ::= character_substring_function

character_substring_function ::=
        CONCAT LEFT_PAREN string_value_expression COMMA  (character_substring_function | string_value_expression) RIGHT_PAREN

value_expression ::=  NUMBER 
                    | QUESTION_MARK? column_reference 
                    | LEFT_BRACE column_reference (LEFT_BRACKET lang RIGHT_BRACKET)? COLON? column_reference? RIGHT_BRACE

lang ::= IDENTIFIER

private identifier_chain ::= (IDENTIFIER | TABLE_NAME_IDENTIFIER | COLUMN_REFERENCE_IDENTIFIER) [ { (DOT|COLON) (IDENTIFIER | COLUMN_REFERENCE_IDENTIFIER)  }* ]

column_reference ::= identifier_chain

group_by_clause ::= GROUP BY [ set_quantifier ] grouping_element_list

grouping_element_list ::= grouping_element [ { COMMA grouping_element }* ]

grouping_element ::= ordinary_grouping_set

ordinary_grouping_set ::= grouping_column_reference | LEFT_PAREN grouping_column_reference_list RIGHT_PAREN

grouping_column_reference ::= value_expression

grouping_column_reference_list ::= grouping_column_reference [ { COMMA grouping_column_reference }* ]
