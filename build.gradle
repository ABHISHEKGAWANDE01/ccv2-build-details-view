/*
 * This file is part of "hybris integration" plugin for Intellij IDEA.
 * Copyright (C) 2014-2016 Alexander Bartash <AlexanderBartash@gmail.com>
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as
 * published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program. If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    ext.kotlin_version = '1.1.51'

    repositories {
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        mavenCentral()
    }//noinspection GroovyAssignabilityCheck
    dependencies {
        classpath "org.jetbrains.intellij.plugins:gradle-intellij-plugin:0.2.0-SNAPSHOT"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    mavenCentral()
}

allprojects {
    buildDir = customBuildDir

    apply plugin: 'java'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

    //noinspection GroovyAssignabilityCheck
    sourceSets {
        //noinspection GroovyAssignabilityCheck
        main {
            java.srcDirs 'src', 'gen'
            resources.srcDirs 'resources'
        }

        test {
            java.srcDir 'tests'
        }
    }

    // https://github.com/JetBrains/gradle-intellij-plugin
    apply plugin: 'org.jetbrains.intellij'
    intellij {
        type 'IU'
        //@see https://www.jetbrains.com/intellij-repository/releases (or ../snapshots) for possible values
        version '172.3544.35'
        downloadSources true
        updateSinceUntilBuild false

        sandboxDirectory "$buildDir/idea-sandbox"

        pluginName 'ccom.intellij.idea.plugin.hybris.impex'
        plugins = ['ant', 'Spring', 'uml', 'junit', 'JavaEE', 'maven', 'eclipse', 'gradle']

        runIde {
            jvmArgs "-Xmx2G"
        }

        runIdea {
            jvmArgs "-Xmx2G"
        }
    }
    patchPluginXml {
        sinceBuild '172'
    }

    task copyPsiViewer(type: Copy) {
        from "$projectDir/PsiViewer.jar"
        into "$buildDir/idea-sandbox/plugins/"
    }

    prepareSandbox.doLast {copyPsiViewer.execute()}
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: "kotlin"

//noinspection GroovyAssignabilityCheck
dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    compile 'commons-codec:commons-codec:1.10'
    compile 'commons-io:commons-io:2.5'
    compile 'com.google.guava:guava:22.0'
    compile 'com.google.code.findbugs:jsr305:3.0.2'
    compile 'org.apache.commons:commons-lang3:3.6'
    compile 'org.apache.commons:commons-collections4:4.1'
    compile 'org.apache.maven:maven-model:3.5.0'
    compile 'com.wutka:dtdparser:1.21'

    compile 'com.github.ben-manes.caffeine:caffeine:2.5.3'
    compile 'org.jsoup:jsoup:1.10.3'

    //this should be pulled from <idea-home>/lib/ant/lib
    compileOnly 'org.apache.ant:ant:1.9.4'

    testCompile 'org.mockito:mockito-core:2.8.47'

    compile project(':common')
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}