buildscript {
    ext.kotlin_version = '1.1.2'
    
    repositories {
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        mavenCentral()
    }//noinspection GroovyAssignabilityCheck
    dependencies {
        classpath "org.jetbrains.intellij.plugins:gradle-intellij-plugin:0.2.0-SNAPSHOT"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    mavenCentral()
}

allprojects {
    buildDir = 'gradle-build'

    apply plugin: 'java'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

    //noinspection GroovyAssignabilityCheck
    sourceSets {
        //noinspection GroovyAssignabilityCheck
        main {
            java.srcDirs 'src', 'gen'
            resources.srcDirs 'resources'
        }

        test {
            java.srcDir 'tests'
        }
    }
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: "kotlin"

//noinspection GroovyAssignabilityCheck
dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    
    compile 'commons-codec:commons-codec:1.10'
    compile 'commons-io:commons-io:2.5'
    compile 'com.google.guava:guava:19.0'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-collections:commons-collections:3.2.2'
    compile 'org.apache.maven:maven-model:3.3.9'
    compile 'com.wutka:dtdparser:1.21'

    compile 'com.github.ben-manes.caffeine:caffeine:2.4.0'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.10.2'

    //this should be pulled from <idea-home>/lib/ant/lib
    compileOnly 'org.apache.ant:ant:1.9.4'

    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.1.0'

}

// https://github.com/JetBrains/gradle-intellij-plugin
apply plugin: 'org.jetbrains.intellij'
intellij {
    type 'IU'
    //@see https://www.jetbrains.com/intellij-repository/releases (or ../snapshots) for possible values
    version '172.2103.15'
    downloadSources true
    updateSinceUntilBuild false

    sandboxDirectory "$buildDir/idea-sandbox"

    pluginName 'ccom.intellij.idea.plugin.hybris.impex'
    plugins = ['ant', 'Spring', 'uml', 'junit', 'JavaEE', 'maven', 'eclipse']
}
patchPluginXml {
    sinceBuild '172'
}
